HEX

FE200000 CONSTANT GPFSEL0
FE20001C CONSTANT GPSET0
FE200028 CONSTANT GPCLR0
FE200034 CONSTANT GPLEV0
FE003004 CONSTANT SYSCLO
FE200008 CONSTANT GPFSEL2
FE200004 CONSTANT GPFSEL1

80 CONSTANT LCDLN1
C0 CONSTANT LCDLN2

DECIMAL

25 CONSTANT LCDRS
24 CONSTANT LCDE       
12 CONSTANT LCD0      
4  CONSTANT LCD1      
27 CONSTANT LCD2      
16 CONSTANT LCD3      
23 CONSTANT LCD4      
17 CONSTANT LCD5      
18 CONSTANT LCD6      
22 CONSTANT LCD7


0 CONSTANT GPIO0
6 CONSTANT GPIO6
13 CONSTANT GPIO13
21 CONSTANT GPIO21
20 CONSTANT GPIO20



: NOW SYSCLO @ ;

: DELAY NOW + BEGIN DUP NOW - 0 <= UNTIL DROP ;

: MASK   3 * 7 SWAP LSHIFT ;

: SET   DUP 10 /MOD 4 * GPFSEL0 + DUP @ ROT MASK INVERT AND 2DUP SWAP ! ;

: OUTPUT   ROT 10 MOD 3 * 1 SWAP LSHIFT OR SWAP ! ;

: INPUT   ROT 10 MOD 3 * 1 SWAP LSHIFT INVERT AND ! ;

: ACCESO   1 SWAP LSHIFT GPSET0 ! ;

: SPENTO   1 SWAP LSHIFT GPCLR0 ! ;

\ ( GPIOn -- LEVn )
: LEVEL   1 SWAP LSHIFT GPLEV0 @ SWAP AND ;

\ ---------- LCD ---------------

\ Effettua il set dei pin dell'LCD ad OUTPUT
\ ( -- )
: LCD-SETUP
	LCDRS SET OUTPUT
	LCDE SET OUTPUT
	LCD0 SET OUTPUT
	LCD1 SET OUTPUT
	LCD2 SET OUTPUT
	LCD3 SET OUTPUT
	LCD4 SET OUTPUT
	LCD5 SET OUTPUT
	LCD6 SET OUTPUT
	LCD7 SET OUTPUT 
;

( -- )
: LCDEON LCDE ACCESO ;

( -- )
: LCDEOFF LCDE SPENTO ;

HEX


\ CHECKBIT ( n1 n2 -- flag )
: CHECKBIT AND 0<> ;


\ ?LCDSET ( flag pin -- ) 
: ?LCDSET SWAP IF ACCESO ELSE SPENTO THEN ;


( n -- )
: LCDWRITE 
	DUP 100 CHECKBIT LCDRS ?LCDSET LCDEON 
	DUP 80 CHECKBIT LCD7 ?LCDSET 
	DUP 40 CHECKBIT LCD6 ?LCDSET
	DUP 20 CHECKBIT LCD5 ?LCDSET 
	DUP 10 CHECKBIT LCD4 ?LCDSET 
	DUP 08 CHECKBIT LCD3 ?LCDSET 
	DUP 04 CHECKBIT LCD2 ?LCDSET 
	DUP 02 CHECKBIT LCD1 ?LCDSET 
	DUP 01 CHECKBIT LCD0 ?LCDSET 
	DROP LCDEOFF
;


\ Pulisce il display scrivendo il carattere SPAZIO su tutta la DDRAM e riportando il puntantore all'inizio.
( -- ) 
: LCDCLR 1 LCDWRITE ;


\ Effettua l'inizializzazione dell'LCD settando le impostazioni desiderate.
( -- )
: LCD-INIT
	LCD-SETUP 

	38 LCDWRITE
	500 DELAY

	6 LCDWRITE 
	500 DELAY

	C LCDWRITE 
	500 DELAY 
	
	LCDCLR 
;


DECIMAL


\ LCDNTYPE ( n -- ) Stampa un numero sull'LCD.
: LCDNTYPE BEGIN 10 /MOD SWAP 304 + LCDWRITE DUP 0= UNTIL DROP ;


HEX


\ LCDSTYPE ( addr_string lenght_string -- ) Stampa una stringa sull'LCD.
: LCDSTYPE OVER + SWAP BEGIN 2DUP <> WHILE DUP c@ 100 + LCDWRITE 1+ REPEAT 2DROP ;


\ LCDLN
( n -- addr_line )
: LCDLN 1 = IF LCDLN1 ELSE LCDLN2 THEN ;


( n -- )
: LCDLN! LCDLN + LCDWRITE ;


( -- )
: LCDCURL>R 6 LCDWRITE ;


( -- )
: LCDCURL<R 4 LCDWRITE ;


\ LCDNUMBER
\ ( number offset line -- )
: LCDNUMBER LCDLN! LCDCURL<R LCDNTYPE ;


\ LCDSTRING
\ ( addr_string lenght_string offset line -- )
: LCDSTRING LCDLN! LCDCURL>R LCDSTYPE ;


